/* Code Base Part 2:
   Snake met staart + punt

   Eindresultaat:
   Snake op een 8x8 LED matrix + joystick

   21-6-2021
   Door Collin Kasbergen
*/

#include <LedControl.h>

/* Joystick PINs */
#define VRX     A0
#define VRY     A1
#define SW      2

/* Display PINs */
#define CLK     8
#define CS      9
#define DIN     10

#define MAAT    8
#define STAP_DELAY 50

#define MAXLENGTE 12

int lengte = 1;
int snake[MAXLENGTE][2];

int punt[2];
double timer;
int blinkSnelheid = 300;

LedControl lc = LedControl(DIN, CLK, CS, 1);

void setup() {
  pinMode(SW, INPUT_PULLUP);
  pinMode(VRX, INPUT);
  pinMode(VRY, INPUT);

  lc.shutdown(0, false);
  lc.setIntensity(0, 8);

  startGame();

  Serial.begin(9600);
}

void loop() {
  for (int i = 0; i < lengte; i++) {
    Serial.print(i);
    Serial.print(",");
    Serial.print(snake[i][0]);
    Serial.print(",");
    Serial.println(snake[i][1]);
  }
  leegScherm();
  leesJoystick();
  verplaatsSnake();
  checkPunt();
  //vulDeStaart();
  tekenGame();
}

void startGame() {
  randomSeed(analogRead(A5));
  int snakeX = random((MAAT - 1));
  int snakeY = random((MAAT - 1));

  snake[0][0] = snakeX;
  snake[0][1] = snakeY;

  int puntX = random((MAAT - 1));
  int puntY = random((MAAT - 1));

  while (puntX == snakeX && puntY == snakeY) {
    puntX = random((MAAT - 1));
    puntY = random((MAAT - 1));
  }

  timer = millis();
}

void leegScherm() {
  for (int x = 0; x < MAAT; x++) {
    for (int y = 0; y < MAAT; y++) {
      lc.setLed(0, x, y, 0);
    }
  }
}

void leesJoystick() {
  int verplaatsingX = map(analogRead(VRX), 0, 1023, -2, 2);
  int verplaatsingY = map(analogRead(VRY), 0, 1023, -2, 2);

  if (verplaatsingX == 2) {
    vulDeStaart();
    snake[0][1]--;
  }
  if (verplaatsingX == -2) {
    vulDeStaart();
    snake[0][1]++;
  }
  if (verplaatsingY == 2) {
    vulDeStaart();
    snake[0][0]--;
  }
  if (verplaatsingY == -2) {
    vulDeStaart();
    snake[0][0]++;
  }
}

void verplaatsSnake() {
  if (snake[0][0] > (MAAT - 1)) {
    snake[0][0] = 0;
  }

  if (snake[0][1] > (MAAT - 1)) {
    snake[0][1] = 0;
  }

  if (snake[0][0] < 0) {
    snake[0][0] = (MAAT - 1);
  }

  if (snake[0][1] < 0) {
    snake[0][1] = (MAAT - 1);
  }
}

void checkPunt() {
  if (punt[0] == snake[0][0] && punt[1] == snake[0][1]) {
    while (punt[0] == snake[0][0] && punt[1] == snake[0][1]) {
      punt[0] = random((MAAT - 1));
      punt[1] = random((MAAT - 1));
    }
    lengte++;
  }
}

void vulDeStaart() {
  for (int teller = lengte; teller > 0; teller--) {
    snake[teller][0] = snake[teller - 1][0];
    snake[teller][1] = snake[teller - 1][1];
  }
}

void tekenGame() {
  for (int teller = 0; teller < lengte; teller++) {
    int x = snake[teller][0];
    int y = snake[teller][1];
    lc.setLed(0, x, y, 1);
  }
  tekenPunt();
  delay(STAP_DELAY);
}

void tekenPunt() {
  if(millis() - timer < blinkSnelheid/2){
    lc.setLed(0, punt[0], punt[1], 1);
  }
  if(millis() - timer > blinkSnelheid/2){
    lc.setLed(0, punt[0], punt[1], 0);
  }
  if(millis() - timer > blinkSnelheid){
    timer = millis();
  }
}
